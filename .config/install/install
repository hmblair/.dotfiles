#!/usr/bin/env zsh

# Parse arguments
FORCE=0
for arg in "$@"; do
    case "$arg" in
        -f|--force) FORCE=1 ;;
    esac
done

INSTALL_DIR="$HOME/.config/install"
LOG_DIR="$HOME/.local/log/install"
mkdir -p "$LOG_DIR"

# Colors
_GREEN='\033[0;32m'
_RED='\033[0;31m'
_CYAN='\033[0;36m'
_NC='\033[0m'

# Install configuration (use env vars from .zprofile, with fallbacks)
: "${GLOBAL_PREFIX:=/usr/local}"
: "${LOCAL_PREFIX:=$HOME/.local}"
export GLOBAL_PREFIX LOCAL_PREFIX
export JOBS=$(nproc 2>/dev/null || sysctl -n hw.ncpu 2>/dev/null || echo 1)

# Global installs disabled by default (run install script directly with --global to enable)
HAS_SUDO=0
for arg in "$@"; do
    [[ "$arg" == "--global" ]] && HAS_SUDO=1
done

# Map script names to: command(s) to check, global (1=sudo, 0=local)
# Multiple commands separated by space - script runs if ANY are missing
declare -A INSTALL_CMDS=(
    [btop]=btop
    [fzf]="fzf fd"
    [l]=l
    [ninja]=ninja
    [nvim]=nvim
    [oh-my-posh]=oh-my-posh
    [stow]=stow
    [zsh]=zsh
    [zsh-plugins]=
)
declare -A INSTALL_GLOBAL=(
    [btop]=1
    [fzf]=0
    [l]=0
    [ninja]=1
    [nvim]=1
    [oh-my-posh]=0
    [stow]=0
    [zsh]=1
    [zsh-plugins]=0
)

# Check if any command in a space-separated list is missing
any_missing() {
    for cmd in ${=1}; do
        command -v "$cmd" &>/dev/null || return 0
    done
    return 1
}

# Find what needs installing, separate global vs local
typeset -a GLOBAL_INSTALLS LOCAL_INSTALLS
for script in "$INSTALL_DIR"/*; do
    name="$(basename "$script")"
    [[ "$name" == "install" || "$name" == "zu" ]] && continue
    [[ -f "$script" && -x "$script" ]] || continue

    cmds="${INSTALL_CMDS[$name]}"
    if [[ -n "$cmds" ]] && { (( FORCE )) || any_missing "$cmds"; }; then
        if (( INSTALL_GLOBAL[$name] && HAS_SUDO )); then
            GLOBAL_INSTALLS+=("$name")
        else
            LOCAL_INSTALLS+=("$name")
        fi
    elif [[ -z "$cmds" ]]; then
        source "$script" > "$LOG_DIR/$name.log" 2>&1
    fi
done

# Run global installs in a single sudo shell
if (( ${#GLOBAL_INSTALLS[@]} > 0 )); then
    printf "${_CYAN}global install:${_NC} %s\n" "${(j:, :)GLOBAL_INSTALLS}"
    printf "${_CYAN}(Ctrl+C to skip global installs)${_NC}\n"

    # Install brew dependencies before sudo (Homebrew should not run as root)
    if [[ "$OSTYPE" == "darwin"* ]] && command -v brew &>/dev/null; then
        for name in "${GLOBAL_INSTALLS[@]}"; do
            case "$name" in
                btop) brew install coreutils make gcc lowdown 2>/dev/null ;;
            esac
        done
    fi

    # Build script to run as root (pass only necessary env vars for security)
    sudo zsh -c "
        export PREFIX='$GLOBAL_PREFIX'
        export LOCAL_PREFIX='$LOCAL_PREFIX'
        export GLOBAL_PREFIX='$GLOBAL_PREFIX'
        export USE_SUDO=1
        export JOBS=$JOBS
        export PATH='$PATH'
        export CC='${CC:-}'
        export CXX='${CXX:-}'
        export CFLAGS='${CFLAGS:-}'
        export CXXFLAGS='${CXXFLAGS:-}'
        export CPPFLAGS='${CPPFLAGS:-}'
        export LDFLAGS='${LDFLAGS:-}'

        for name in ${(j: :)GLOBAL_INSTALLS}; do
            log_file='$LOG_DIR/'\$name'.log'
            printf '  %-20s' \"\$name\"
            if source '$INSTALL_DIR/'\$name > \"\$log_file\" 2>&1; then
                printf '\033[0;32m%s\033[0m\n' '$GLOBAL_PREFIX/bin/'\"\${name}\"
            else
                printf '\033[0;31m%s\033[0m\n' 'FAILED (see '\"\$log_file\"')'
            fi
        done
    "
fi

# Run local installs normally
if (( ${#LOCAL_INSTALLS[@]} > 0 )); then
    printf "${_CYAN}local install:${_NC} %s\n" "${(j:, :)LOCAL_INSTALLS}"

    export PREFIX="$LOCAL_PREFIX"
    export USE_SUDO=0

    for name in "${LOCAL_INSTALLS[@]}"; do
        cmds="${INSTALL_CMDS[$name]}"
        log_file="$LOG_DIR/$name.log"

        printf "  %-20s" "$name"
        if source "$INSTALL_DIR/$name" > "$log_file" 2>&1; then
            printf "${_GREEN}%s${_NC}\n" "OK"
        else
            printf "${_RED}%s${_NC}\n" "FAILED (see $log_file)"
        fi
    done
fi
